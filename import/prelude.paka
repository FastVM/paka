
macro type.none {0}
macro type.boolean {1}
macro type.number {2}
macro type.function {3}
macro type.array {4}
macro type.string {5}
macro type.box {6}
macro type.map {7}

macro effect.math.div {1}
macro effect.math.mod {2}
macro effect.type.concat {3}
macro effect.bounds {4}

def puts(str) {
	i := 0
	while i < #str {
		putchar(str[i])
		i = i + 1
	}
}

def print_num(n) {
	if n < 0 {
		puts("-")
		print_num(0 - n)
	} else {
		if n >= 10 {
			lowest := n % 10
			rec((n - lowest) / 10)
			putchar(lowest + 48)
		} else {
			putchar(n % 10 + 48)
		}
	}
}

def print_array(arr) {
	puts("[")
	i := 0
	while i < length(arr) {
		if i != 0 {
			puts(", ")
		}
		print(arr[i])
		i = i + 1
	}
	puts("]")
}

def print(value) {
	if type(value) == type.none {
		puts("none")
	} else: if type(value) == type.boolean {
		if value {
			puts("true")
		} else {
			puts("false")
		}
	} else: if type(value) == type.number {
		print_num(value)
	} else: if type(value) == type.function {
		puts("lambda(...){}")
	} else: if type(value) == type.array {
		print_array(value)
	} else: if type(value) == type.string {
		puts("\"")
		puts(value)
		puts("\"")
	} else: if type(value) == type.box {
		puts("box ")
		print(unbox value)
	} else: if type(value) == type.map {
		puts("(")
		print_num(length(value))
		puts("){")
		puts("...")
		puts("}")
	} else {
		puts("?")
	}
}

def println(value) {
	print(value)
	putchar(10)
}

handle effect.math.div {
	println("unhandled: div by zero")
	exit
}

handle effect.math.mod {
	println("unhandled: mod by zero")
	exit
}

handle effect.type.concat {
	println("unhandled: type error")
	exit
}

handle effect.bounds {
	println("unhandled: index out of bounds")
	exit
}
