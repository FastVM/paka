
import("../lang/io.paka")
import("../lang/conv.paka")
import("../lang/file.paka")
import("../lang/stream.paka")
import("../lang/strs.paka")

import("macros.paka")
import("findvar.paka")
import("paka/all.paka")
import("pass/all.paka")
import("ir/ir.paka")
import("emit.paka")

import("util/ast.paka")


def vm_set(s) {
    return [vm.ast.do, ["=", [vm.ast.ident, "_ret"], s]]
}

def vm_put() {
    return [vm.ast.if, ["!=", [vm.ast.ident, "_ret"], [vm.ast.none]], [vm.ast.call, [vm.ast.ident, "println"], [vm.ast.ident, "_ret"]], [vm.ast.do]]
}

def vm_exec(n, bc, rest) {
    i = 0
    while i < n {
        exec(bc, rest)
        i = i + 1
    }
}

def main(argv) {
    run = false
    outfile = none
    srcfile = none
    rest = []
    outlang = "minivm"
    ntimes = 1
    i = 0
    while i < length(argv) {
        arg = argv[i]
        if run {
            rest ~= [arg]
            i = i + 1
        } else {
            if arg[0] == '-' {
                if arg[1] == '-' {
                    run = true
                } 
                if arg[1] == 'o' {
                    i = i + 1
                    outfile = argv[i]
                }
                if arg[1] == 'l' {
                    i = i + 1
                    outlang = argv[i]
                }
                if arg[1] == 'n' {
                    i = i + 1
                    ntimes = to_int(argv[i])
                }
                if arg[1] == 'e' {
                    i = i + 1
                    ast1 = paka_parse("none " ~ argv[i])
                    ast2 = [vm.ast.do, vm_set(ast1), vm_put()]
                    bc = emit(ast2, ['/'])
                    i = i + 1
                    while i < length(argv) {
                        rest ~= [argv[i]]
                        i = i + 1
                    }
                    vm_exec(ntimes, bc, rest)
                    return none
                }
            } else {
                if srcfile != none {
                    puts("only one source can be provided")
                    exit
                }
                srcfile = arg
            }
            i = i + 1
        }
    }
    if srcfile == none {
        puts("no sources provided")
        exit
    }
    src = read(srcfile)
    if src == none {
        puts("cannot read file")
        exit
    }
    ast = paka_parse(src)
    if outfile == none {
        if outlang == "minivm" {
            outfile = "exec.bc"
        }
    }
    if run {
        if outlang != "minivm" {
            puts("cannot directly run any lang other than `-l minivm`")
            exit
        }
        bc = emit(ast, srcfile)
        vm_exec(ntimes, bc, rest)
    } else {
        if outlang == "minivm" {
            bc = emit(ast, srcfile)
            dump(outfile, bc)
            exit
        }
    }
}

main(args)
