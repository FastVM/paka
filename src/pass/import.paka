
def pass_import_copy(form, state) {
    ret = [form[0]]
    i = 1
    while i < length(form) {
        ret ~= [pass_import_state(form[i], state)]
        i = i + 1
    }
    return ret
}

def pass_import_resolve_import(form, state) {
    name = state[0] ~ ['/'] ~ form[1][1]
    i = 0
    while i < length(state[1]) {
        existing = state[1][i]
        if existing == name {
            return [vm.ast.do]
        }
        i = i + 1
    }
    state[1] ~= [name]
    src = read(name)
    if src == none {
        puts("cannot open file: " ~ name)
        exit
    }
    ast = paka_parse(src)
    xstate = [file_dir(name), state[1]]
    full = pass_import_state(ast, xstate)
    state[1] = xstate[1]
    return full
}

def pass_import_state(form, state) {
    ty = form[0]
    if util_ast_is_literal(ty) {
        return form
    }
    if ty == vm.ast.ident {
        return form
    }
    if ty == vm.ast.import {
        return pass_import_resolve_import(form, state)
    }
    return pass_import_copy(form, state)
}

def pass_import(form, path) {
    dir = file_dir(path)
    state = [dir, []]
    ret = pass_import_state(form, state)
    return ret
}
